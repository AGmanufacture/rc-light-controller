// Lichthupe am Lenkungskanal (ST)
//
// Wenn bei Verwendung des 5-Kanal Pre-Processor die Blinker manuell geschaltet
// werden, ist der Lenkungskanal (ST) unbenutzt.
// Wenn gewünscht kann man dann einen Empfängerausgang, der am Sender durch
// eine Taste gesteuert wird, an ST anschliessen und als Lichthupe benutzen.
//
//
// Das Programm arbeitet wie folgt:
// Wenn die Lichthupe betätigt wird, schalten wir die Fernlichter ein.
//
// In Lichtschalterstellung 3 und 4 ist das Fernlicht permanent an, da schalten
// wir es kurz aus wenn die Lichthube betätigt wird.
//
// In allen Lichtschalterstellungen beobachten wir das ST signal.
// Wenn dessen Wert auf größer als 50 geht (also Taste gedrückt wurde), dann
// schalten wir das Fernlicht ein, ansonst aus.
//
// ACHTUNG: Das Light Program übernimmt die Funktion der Fernlicht LEDs 4 und 5.
// der Eintrag in der Tabelle ist daher ohne Wirkung!

run when light-switch-position-0 \
      or light-switch-position-1 \
      or light-switch-position-2

led highbeam_l = led[4]
led highbeam_r = led[5]

var aux-position

loop:
    sleep 20
    aux-position = steering
    skip if aux-position > 50
    goto off

on:
    highbeam_l, highbeam_r = 100
    goto loop

off:
    highbeam_l, highbeam_r = 0
    goto loop

end

run when light-switch-position-3 \
      or light-switch-position-4

led highbeam_l = led[4]
led highbeam_r = led[5]

var aux-position

loop:
    sleep 20
    aux-position = steering
    // Umgekehrte Logik: wir schalten das Fernlicht aus wenn die Lichthupe
    // benützt wird
    skip if aux-position < 50
    goto off

on:
    highbeam_l, highbeam_r = 100
    goto loop

off:
    highbeam_l, highbeam_r = 0
    goto loop

end
