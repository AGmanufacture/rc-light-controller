// Light program that makes 3 LEDs flicker
run when light-switch-position-1

// Choose which LEDs you want to have flickering
led l1 = led[12]
led l2 = led[13]
led l3 = led[14]
const NUMBER_OF_LEDS = 3

// MIN_VALUE defines the darkest light level during flickering
// RANGE + MIN_VALUE give the brightes light leve.
//
// IMPORTANT: to simplify the code the value of
// RANGE must be a power of 2 (e.g. 2, 4, 8, 16 ...) minus 1
const MIN_VALUE = 10
const RANGE = 15

var counter
var value
var range

loop:
    // Get a random value, limit it to RANGE, and add the minimum brightness
    value = random
    value &= RANGE
    value += MIN_VALUE

    // Make the leds to change brightness slowly
    fade all leds stepsize 2

    // Depending on the counter (from 0..NUMBER_OF_LEDS) set the value
    // to the appropriate LED. We do this so that we don't have to
    // duplicate the randomization code above.
    skip if counter != 0
    l1 = value
    skip if counter != 1
    l2 = value
    skip if counter != 2
    l3 = value

    // Increment the counter and re-run the loop until we are done
    // with all LEDs
    counter += 1
    skip if counter == NUMBER_OF_LEDS
    goto loop

    // Wait a bit and start all-over
    sleep 20
    counter = 0
    goto loop

end
