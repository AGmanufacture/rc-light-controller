// Headlamp flasher on AUX2
//
// The headlamp flasher script works as follows:
// Whenever the headlamp flasher is triggered, we temporarily switch
// to light switch position 4 (where all lamps are on)
//
// In all light switch positions EXCEPT 4, we monitor the AUX2 switch.
// When its value goes above 50, (i.e. it is depressed) then we save the
// current light switch position into a global variable and switch
// to light switch position 4.
// The script then ends.
//
// Since we switched the light switch position to 4, now a second script
// runs. In this script we monitor AUX 2 going below 50 (= released). When
// released we restore the saved light switch position from the global
// variable.
// We also use a global flag headlamp-flasher-active to ensure that the
// light-switch-position-4 light program restores the light switch position
// only when this flag is set, i.e. the headlight flasher was activated.

run when light-switch-position-0 \
      or light-switch-position-1 \
      or light-switch-position-2 \
      or light-switch-position-3

global var last-light-switch-position
global var headlamp-flasher-active
var aux-position

loop:
    sleep 20
    aux-position = aux2
    skip if aux-position > 50
    goto loop

    last-light-switch-position = light-switch-position
    headlamp-flasher-active = 1
    light-switch-position = 4
end


run when light-switch-position-4

global var last-light-switch-position
var aux-position

loop:
    sleep 20
    skip if headlamp-flasher-active > 0
    goto loop

    aux-position = aux2
    skip if aux-position < 50
    goto loop

    light-switch-position = last-light-switch-position
    headlamp-flasher-active = 0
end