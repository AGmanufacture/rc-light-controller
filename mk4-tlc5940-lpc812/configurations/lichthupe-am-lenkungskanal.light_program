// Lichthupe am Lenkungskanal (ST)
//
// Wenn bei Verwendung des 5-Kanal Pre-Processor die Blinker manuell geschaltet
// werden, ist der Lenkungskanal (ST) unbenutzt.
// Wenn gewünscht kann man dann einen Empfängerausgang, der am Sender durch
// eine Taste gesteuert wird, an ST anschliessen und als Lichthupe benutzen.
//
//
// Das Programm arbeitet wie folgt:
// Immer wenn die Lichthupe betätigt wird, schalten wir temporär den
// Lichtschalter auf Stellung 4 (alle Lichter an)
//
// In allen Lichtschalterstellungen außer 4 beobachten wir das ST signal.
// Wenn dessen Wert auf größer als 50 geht (also Taste gedrück wurde), dann
// speichern wir die derzeitige Lichtschalterstellung in eine globale Variable
// und schalten den Lichtschalter auf Stellung 4.
// Damit endet das Programm.
//
// Der Lichtschalter steht jetzt also auf Stellung 4, und auf dieser Stellung
// lassen wir ein anderes Programm laufen. Dieses beobachtet auch wieder
// das ST Signal. Wenn der Wert von ST weniger als 50 ist (Taste losgelassen)
// dann stellen wir die ursprüngliche Lichtschalterstellung, die wir ja oben
// in einer globalen Variable gespeichert haben, her.
//
// Damit wir Lichtschalterstellung 4 auch manuell einschalten können, verwenden
// wir eine weitere globale Variable die wir nur setzten, wenn der Lichtschalter
// durch die Lichthupe auf Stellung 4 gesetzt wurde. Damit verhindern wir
// dass das zweite Programm den Lichtschalter irrtümlich "zurücksetzt" wenn wir
// den Lichtschalter auf position 4 schalten.


run when light-switch-position-0 \
      or light-switch-position-1 \
      or light-switch-position-2 \
      or light-switch-position-3

global var last-light-switch-position
global var headlamp-flasher-active
var aux-position

loop:
    sleep 20
    aux-position = steering
    skip if aux-position > 50
    goto loop

    last-light-switch-position = light-switch-position
    headlamp-flasher-active = 1
    light-switch-position = 4
end


run when light-switch-position-4

global var last-light-switch-position
var aux-position

loop:
    sleep 20
    skip if headlamp-flasher-active > 0
    goto loop

    aux-position = steering
    skip if aux-position < 50
    goto loop

    light-switch-position = last-light-switch-position
    headlamp-flasher-active = 0
end